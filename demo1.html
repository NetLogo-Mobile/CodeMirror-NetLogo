<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf8" />
    <h1>New Code Editor for NetLogo Web & Derivatives</h1>
  </head>
  <body>
    <button name="Pretty" onclick="Editor.Prettify();">Pretty</button>
    <button name="PrettyAll" onclick="Editor.PrettifyAll();">Pretty All</button>
    <div id="Container" style="height: 100%"></div>
    <div id="Code"></div>
    <script src="editor.bundle.js"></script>
    <script>
      var Editor;
      window.addEventListener('load', function () {
        GalapagosEditor.DebugEnabled = true;
        Editor = new GalapagosEditor(document.getElementById('Container'), {
          OnUpdate: (Changed, Update) => {
            if (Changed) console.log(Update);
          },
        });
        EditorDictionary.Initialize({});
        Editor.SetWidgetVariables(["num-ants","start-delay","leader-wiggle-angle"]);
Editor.SetCode(`breed [ leaders leader ]
breed [ followers follower ]

globals [
  nest-x nest-y    ;; location of center of nest
  food-x food-y    ;; location of center of food
]

to setup
  clear-all
  set-default-shape turtles "bug"
  set nest-x 10 + min-pxcor                      ;; set up nest and food locations
  set nest-y 0
  set food-x max-pxcor - 10
  set food-y 0
  ;; draw the nest in brown by stamping a circular
  ;; brown turtle
  ask patch nest-x nest-y [
    sprout 1 [
      set color brown
      set shape "circle"
      set size 10
      stamp
      die
    ]
  ]
  ;; draw the food in orange by stamping a circular
  ;; orange turtle
  ask patch food-x food-y [
    sprout 1 [
      set color orange
      set shape "circle"
      set size 10
      stamp
      die
    ]
  ]
  create-leaders 1
    [ set color red ]                              ;; leader ant is red and start with a random heading
  create-followers (num-ants - 1)
    [ set color yellow                             ;; middle ants are yellow
      set heading 90 ]                             ;; and start with a fixed heading
  ask turtles
    [ setxy nest-x nest-y                          ;; start the ants out at the nest
      set size 2 ]
  ask turtle (max [who] of turtles)
    [ set color blue                               ;; last ant is blue
      set pen-size 2
      pen-down ]                                   ;; ...and leaves a trail
  ask leaders
    [ set pen-size 2
      pen-down ]                                   ;; the leader also leaves a trail
  reset-ticks
end

to go
  if all? turtles [xcor >= food-x]
    [ stop ]
   ask leaders                                      ;; the leader ant wiggles and moves
     [ wiggle leader-wiggle-angle
       correct-path
       if (xcor > (food-x - 5 ))                    ;; leader heads straight for food, if it is close
         [ facexy food-x food-y ]
       if xcor < food-x                             ;; do nothing if you're at or past the food
         [ fd 0.5 ] ]
   ask followers
     [ face turtle (who - 1)                        ;; follower ants follow the ant ahead of them
       if time-to-start? and (xcor < food-x)        ;; followers wait a bit before leaving nest
         [ fd 0.5 ] ]
  tick
end

;; turtle procedure; wiggle a random amount, averaging zero turn
to wiggle [angle]
  rt random-float angle
  lt random-float angle
end

;; turtle procedure
to correct-path
  ifelse heading > 180
    [ rt 180 ]
    [ if patch-at 0 -5 = nobody
        [ rt 100 ]
     if patch-at 0 5 = nobody
        [ lt 100 ] ]
end

;; turtle reporter; if true, then the ant is authorized to move out of the nest
to-report time-to-start?
  report ([xcor] of (turtle (who - 1))) > (nest-x + start-delay + random start-delay )
end


; Copyright 1997 Uri Wilensky.
; See Info tab for full copyright and license.
`)      });
    </script>
  </body>
</html>
