@precedence {
  operation @left,
  open @left
}

@top Program { 
  globalStatement*
  Procedure*
  Unrecognized*
}

@detectDelim

@skip { space | LineComment }

Unrecognized {
  expression
}

VariableDeclaration {
  NewVariableDeclaration |
  SetVariable
}

NewVariableDeclaration {
  Let Identifier Value
}

SetVariable {
  Set VariableName Value
}

VariableName {
  Identifier | PatchVar | TurtleVar | LinkVar
}

Value {
  Calculation |
  String |
  OpenBracket Value* CloseBracket 
}

NumericValue {
  Constant |
  Numeric |
  VariableName |
  OpenBracket VariableName !open CloseBracket "of" Identifier
}

Procedure {
  To ProcedureName Arguments? ProcedureContent* End
}

AnonymousProcedure {
  OpenBracket Arguments "->" ProcedureContent* CloseBracket
}

Primitive {
  ValFirstPrimitive Value Reporters |
  ValLastPrimitive Reporters Value
}

Reporters {
  Reporter | AnonymousProcedure
}

ProcedureName {
  Identifier
}

Arguments {
  OpenBracket Identifier* CloseBracket
}

ProcedureContent {
  VariableDeclaration | Primitive | expression
}

Calculation {
  NumericValue |
  "(" Calculation ")" |
  Calculation !operation Operation Calculation 
}

globalStatement {
  Extensions { "extensions" OpenBracket Extension* CloseBracket } |

  Globals { "globals" OpenBracket  Identifier* CloseBracket  } |

  Breed { 
    "breed" OpenBracket Identifier Identifier CloseBracket |
    "directed-link-breed" OpenBracket Identifier Identifier CloseBracket |
    "undirected-link-breed" OpenBracket Identifier Identifier CloseBracket
  } |

  BreedsOwn {
    Own OpenBracket Identifier* CloseBracket
  }
}

@tokens {
  Numeric { $[0-9]+ $[\.]? $[0-9]* }
  
  String { '"' (!["\\] | "\\" _)* '"' }

  LineComment { ";" ![\n]* }

  Own { $[A-Za-z]+ "-own" }

  End { "end" }

  To { "to" | "to-report" }

  Let { "let" }

  ValFirstPrimitive {
    "foreach" | "n-values"
  }

  ValLastPrimitive {
    "map" | "reduce" | "filter" | "sort-by"
  }

  Operation {
    "+" | "-" | "/" | "*" | "^"
  }

  space { $[ \t\n\r]+ }

  
  OpenBracket { "[" }
  
  CloseBracket { "]" }

  "[" "]"
}

expression {
  Identifier |
  Command |
  Reporter |
  Numeric |
  String |
  Constant |
  TurtleVar |
  PatchVar |
  LinkVar |
  Unsupported
}

@external tokens keyword from "./tokenizer.js" { Set, Identifier, Directive, Command, Reporter, Extension, TurtleVar, PatchVar, LinkVar, Constant, Unsupported }