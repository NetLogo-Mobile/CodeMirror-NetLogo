@precedence {
  paren @left,
  
  bestleft @left,
  bestright @right,
  best @right,
  cut @cut,
  open @left,
  close @left,
  leftArgs @left,
  decl @left,
  lastArgs @left,
  rightArgs @left,
  worse,
  exponent @right,
  times @left,
  plus @left,
  comparative @left,
  worst @right
}

@top Program { 
  Embedded {
    (CommandStatement | Parenthetical<CommandStatement>)+ ~global
  } |
  Normal {
    (globalStatement | Misplaced | Procedure)* ~global
  } |
  OneLineReporter {
    (ReporterStatement | Value) ~global
  }
}

Parenthetical<content> {
  OpenParen !paren content !paren CloseParen ~parenth
}
WorseParentheticals[@dynamicPrecedence=-2]{
    OpenParen !paren (Value | ReporterStatement | CommandStatement) !paren CloseParen ~parenth
}

@detectDelim

@skip { space | LineComment }

globalStatement {
  Extensions { ExtensionStr OpenBracket Identifier* CloseBracket } |

  Globals { GlobalStr OpenBracket Identifier* CloseBracket  } |

  Breed { BreedStr OpenBracket BreedPlural BreedSingular !cut CloseBracket } |

  BreedsOwn {
    Own OpenBracket Identifier* CloseBracket
  }
}

Misplaced {
  CommandStatement
}

BreedSingular {
  Identifier | SpecialReporter1Args
}

BreedPlural {
  Identifier | SpecialReporter1Args | Reporter0Args
}

Procedure {
  To ProcedureName Arguments? ProcedureContent* End
}

ProcedureName {
  Identifier
}

ProcedureContent {
  CommandStatement |
  Parenthetical<CommandStatement> |
  WorseParentheticals |
  Error {globalStatement | BreedToken OpenBracket BreedPlural BreedSingular CloseBracket} 
}

ReporterContent {
  ReporterStatement
}

VariableDeclaration {
  NewVariableDeclaration |
  SetVariable
}

varValue{
  Value  | ReporterStatement
}

NewVariableDeclaration {
  Let newVariableName !lastArgs varValue
}

SetVariable {
  Set (VariableName|builtinPrims) varValue
}

VariableName {
  Identifier ~identifier | 
  PatchVar | 
  TurtleVar | 
  LinkVar |
  BreedToken |
  UnsupportedPrim ~unsupported |
  SpecialCommandCreateTurtlePossible
}

newVariableName {
  Identifier |
  UnsupportedPrim ~unsupported|
  PatchVar | 
  TurtleVar | 
  LinkVar |
  builtinPrims |
  SpecialCommandCreateTurtlePossible
}

builtinPrims {
  Reporter0Args |
  Reporter1Args |
  Reporter2Args |
  Reporter3Args |
  Reporter4Args |
  Reporter5Args |
  Reporter6Args |
  Reporter0ArgsVar |
  Reporter1ArgsVar |
  Reporter2ArgsVar |
  Reporter3ArgsVar |
  Reporter4ArgsVar |
  Reporter5ArgsVar |
  Reporter6ArgsVar |
  Reporter2ArgsVar0 |
  Reporter1ArgsVar0 |
  ReporterLeft1ArgsOpt |
  ReporterLeft1Args |
  ReporterLeft2Args |
  Command0Args |
  Command1Args |
  Command2Args |
  Command3Args |
  Command4Args |
  Command5Args |
  Command6Args |
  Command0ArgsVar |
  Command1ArgsVar |
  Command2ArgsVar |
  Command3ArgsVar |
  Command4ArgsVar |
  Command5ArgsVar |
  Command6ArgsVar |
  Command3ArgsVar2
}



Arg {  
  Value |
  ReporterStatement |
  ReporterBlock |
  Property
}

Value {
  Parenthetical<Value> |
  WorseParentheticals |
  List |
  CodeBlock |
  VariableName |
  Literal |
  AnonymousProcedure ~anon
}

Literal {
  Numeric ~literal |
  String ~literal |
  Constant ~literal
}

Arguments {
  OpenBracket (Identifier|UnsupportedPrim ~unsupported)* ~bebracket CloseBracket ~endbracket
}

AnonArguments {
  Arguments | (Identifier|UnsupportedPrim ~unsupported)* ~bebracket
}

List { 
  OpenBracket (List | Literal | Identifier)* ~bebracket CloseBracket ~endbracket
}

CodeBlock[@dynamicPrecedence=-1] { 
  OpenBracket (List | CodeBlock | ProcedureContent | Literal | Identifier)* ~bebracket CloseBracket ~endbracket
}

ReporterBlock {
  OpenBracket ReporterStatement  !close ~bebracket CloseBracket ~endbracket |
  OpenBracket Value !close ~bebracket CloseBracket ~endbracket
}

Property {
  OpenBracket VariableName !close ~bebracket CloseBracket ~endbracket
}

AnonymousProcedure {
  OpenBracket AnonArguments Arrow (ProcedureContent*|ReporterStatement*  |Value) ~bebracket CloseBracket ~endbracket
}

ShortAnonymousProcedure {
  OpenBracket (ProcedureContent*|ReporterStatement*  |Value ) ~bebracket CloseBracket ~endbracket
}

leftArg1Phrase {
  (ReporterLeft1Args | ReporterLeft1ArgsOpt) !lastArgs Arg
}


leftArg2Phrase{
  ReporterLeft2Args Arg !lastArgs Arg
}

ReporterStatement { 
  Parenthetical<ReporterStatement> |
  Parenthetical<leftArg1Phrase> |
  Arg !bestleft leftArg1Phrase ~left1 |
  Arg !bestleft leftArg2Phrase ~left2 |
  SpecialReporter0Args | SpecialReporter0ArgsLink | SpecialReporter0ArgsTurtle | SpecialReporter0ArgsLinkP | UnsupportedPrim ~unsupported|
  (Reporter1Args | SpecialReporter1Args | SpecialReporter1ArgsTurtle | SpecialReporter1ArgsLink | SpecialReporter1ArgsBoth) !lastArgs Arg |
  (Reporter2Args | SpecialReporter2Args | SpecialReporter2ArgsTurtle) Arg !lastArgs Arg |
  (Reporter3Args | SpecialReporter3Args) Arg Arg !lastArgs Arg |
  (Reporter4Args | SpecialReporter4Args) Arg Arg Arg !lastArgs Arg |
  (Reporter5Args |  SpecialReporter5Args) Arg Arg Arg Arg !lastArgs Arg |
  (Reporter6Args | SpecialReporter6Args) Arg Arg Arg Arg Arg !lastArgs Arg |
  Reporter0Args |
  (Reporter0ArgsVar) !lastArgs Arg*  |
  (Reporter1ArgsVar | Reporter1ArgsVar0) !lastArgs Arg+ |
  (Reporter2ArgsVar | Reporter2ArgsVar0) Arg !lastArgs Arg+ |
  Reporter3ArgsVar Arg Arg !lastArgs Arg+ |
  Reporter4ArgsVar Arg Arg Arg !lastArgs Arg+ |
  Reporter5ArgsVar Arg Arg Arg Arg !lastArgs Arg+ |
  Reporter6ArgsVar Arg Arg Arg Arg Arg !lastArgs Arg+ |
  Arg !comparative AndOr Arg |
  APReporter Reporters !lastArgs Arg |
  APReporterFlip Arg !lastArgs Reporters |
  APReporterVar Reporters !lastArgs Arg+
}

CommandStatement {
  SpecialCommand0Args | Command0Args |
  UnsupportedPrim  ~unsupported |
  (Command1Args | SpecialCommand1Args) !lastArgs Arg |
  (Command2Args | SpecialCommand2Args) Arg !lastArgs Arg |
  (Command3Args | SpecialCommand3Args) Arg Arg !lastArgs Arg |
  (Command4Args | SpecialCommand4Args) Arg Arg Arg !lastArgs Arg |
  (Command5Args | SpecialCommand5Args) Arg Arg Arg Arg !lastArgs Arg |
  (Command6Args | SpecialCommand6Args) Arg Arg Arg Arg Arg !lastArgs Arg |
  turtle2 { (SpecialCommandCreateTurtle|SpecialCommandCreateTurtlePossible|SpecialCommandCreateLink|Command1_2ArgsOpt) Arg !best Arg } |
  turtle1 { (SpecialCommandCreateTurtle|SpecialCommandCreateTurtlePossible|SpecialCommandCreateLink|Command1_2ArgsOpt) Arg} |
  Command0ArgsVar !lastArgs Arg*  |
  Command1ArgsVar !lastArgs Arg+ |
  Command2ArgsVar Arg !lastArgs Arg+ |
  (Command3ArgsVar | Command3ArgsVar2) Arg Arg !lastArgs Arg+ |
  Command4ArgsVar Arg Arg Arg !lastArgs Arg+ |
  Command5ArgsVar Arg Arg Arg Arg !lastArgs Arg+ |
  Command6ArgsVar Arg Arg Arg Arg Arg !lastArgs Arg+ |
  VariableDeclaration |
  Unsupported |
  APCommand !leftArgs Arg+ !lastArgs Commands
}

Commands {
  SpecialCommand | 
  Command0Args |
  Command1Args |
  Command2Args |
  Command3Args |
  Command4Args |
  Command5Args |
  Command6Args |
  Command0ArgsVar |
  Command1ArgsVar |
  Command2ArgsVar |
  Command3ArgsVar |
  Command4ArgsVar |
  Command5ArgsVar |
  Command6ArgsVar |
  Command3ArgsVar2 |
  SpecialCommand0Args |
  SpecialCommand1Args |
  SpecialCommand2Args |
  SpecialCommand3Args |
  SpecialCommand4Args |
  SpecialCommand5Args |
  SpecialCommand6Args |
  SpecialCommandCreateTurtle |
  SpecialCommandCreateLink |
  AnonymousProcedure ~anon |
  ShortAnonymousProcedure |
  Identifier ~identifier
}

Reporters {
  Reporter0Args |
  Reporter1Args |
  Reporter2Args |
  Reporter3Args |
  Reporter4Args |
  Reporter5Args |
  Reporter6Args |
  Reporter0ArgsVar |
  Reporter1ArgsVar |
  Reporter2ArgsVar |
  Reporter3ArgsVar |
  Reporter4ArgsVar |
  Reporter5ArgsVar |
  Reporter6ArgsVar |
  Reporter2ArgsVar0 |
  Reporter1ArgsVar0 |
  ReporterLeft1ArgsOpt |
  ReporterLeft1Args |
  ReporterLeft2Args !leftArgs |
  SpecialReporter0Args |
  SpecialReporter1Args |
  SpecialReporter2Args |
  SpecialReporter3Args |
  SpecialReporter4Args |
  SpecialReporter5Args |
  SpecialReporter6Args |
  SpecialReporter1ArgsBoth |
  SpecialReporter0ArgsLink |
  SpecialReporter1ArgsLink |
  SpecialReporter2ArgsTurtle |
  SpecialReporter0ArgsTurtle |
  SpecialReporter1ArgsTurtle |
  SpecialReporter0ArgsLinkP |
  AnonymousProcedure |
  ShortAnonymousProcedure |
  Identifier
}

@tokens {

  Numeric { $[-]? $[0-9]+ $[\.]? $[0-9]* ($[e] ($[+] | $[-])? $[0-9]+)? | $[-]? $[\.] $[0-9]+ ($[e] ($[+] | $[-])? $[0-9]+)?}
  
  String { '"' (!["\\] | "\\" _)* '"' }

  LineComment { ";" ![\n]* }

  space { $[ \t\n\r]+ }
  
  OpenBracket { "[" }
  
  CloseBracket { "]" }

  Arrow { "->" }

  OpenParen { "(" }

  CloseParen { ")" }

}


@external tokens keyword from "./tokenizer.js" { 
  ReporterLeft1Args,
  ReporterLeft2Args,
  ReporterLeft1ArgsOpt,  
  GlobalStr, 
  ExtensionStr, 
  BreedStr, 
  Own, 
  Set, 
  Let, 
  To, 
  End, 
  Identifier, 
  Command, 
  Reporter,  
  TurtleVar, 
  PatchVar, 
  LinkVar, 
  Constant, 
  Unsupported,
  SpecialReporter,
  SpecialCommand,
  BreedToken,
  AndOr,
  APCommand,
  APReporterFlip,
  APReporterVar,
  APReporter,
  UnsupportedPrim,
  SpecialCommandCreateTurtle,
  SpecialCommandCreateLink,
  SpecialReporter1ArgsBoth,
  SpecialReporter0ArgsLink,
  SpecialReporter1ArgsLink,
  SpecialReporter2ArgsTurtle,
  SpecialReporter0ArgsTurtle,
  SpecialReporter1ArgsTurtle,
  SpecialReporter0ArgsLinkP,
  SpecialCommandCreateTurtlePossible
}

@external specialize {Reporter} specializeReporter from './specializer' {
  Reporter0Args,
  Reporter1Args, 
  Reporter2Args, 
  Reporter3Args, 
  Reporter4Args,
  Reporter5Args,
  Reporter6Args,
  Reporter0ArgsVar,
  Reporter1ArgsVar, 
  Reporter2ArgsVar, 
  Reporter3ArgsVar, 
  Reporter4ArgsVar,
  Reporter5ArgsVar,
  Reporter6ArgsVar,
  Reporter2ArgsVar0,
  Reporter1ArgsVar0
}

@external specialize {SpecialReporter} specializeSpecialReporter from './specializer' {
  SpecialReporter0Args,
  SpecialReporter1Args,
  SpecialReporter2Args,
  SpecialReporter3Args,
  SpecialReporter4Args,
  SpecialReporter5Args,
  SpecialReporter6Args
}

@external specialize {Command} specializeCommand from './specializer' {
  Command0Args,
  Command1Args, 
  Command2Args, 
  Command3Args, 
  Command4Args,
  Command5Args,
  Command6Args,
  Command0ArgsVar,
  Command1ArgsVar, 
  Command2ArgsVar, 
  Command3ArgsVar, 
  Command4ArgsVar,
  Command5ArgsVar,
  Command6ArgsVar,
  Command3ArgsVar2,
  Command1_2ArgsOpt
}

@external specialize {SpecialCommand} specializeSpecialCommand from './specializer' {
  SpecialCommand0Args,
  SpecialCommand1Args,
  SpecialCommand2Args,
  SpecialCommand3Args,
  SpecialCommand4Args,
  SpecialCommand5Args,
  SpecialCommand6Args
}

@external prop context from './props'
@context contextTracker from './context-tracker'