@precedence {
  leftArgs @left,
  lastArgs @left,
  rightArgs @right,
  exponent @right,
  times @left,
  plus @left,
  comparative @left,
  
  open @left,
  close @left
}

@top Program { 
  normalMode{
    (Unrecognized* globalStatement)*
    Procedure* ~statement
  } |
  OneLineMode{
    ReporterStatement
  } |
  EmbeddedMode{
    ProcedureContent+
  }
  
}

@detectDelim

@skip { space | LineComment }

Unrecognized {
  Procedure ~statement
}

globalStatement {
  Extensions { ExtensionStr OpenBracket Identifier* CloseBracket } |

  Globals { GlobalStr OpenBracket Identifier* CloseBracket  } |

  Breed { BreedDeclarative OpenBracket BreedPlural BreedSingular CloseBracket } |

  BreedsOwn {
    Own OpenBracket Identifier* CloseBracket
  }
}

BreedDeclarative {
  BreedStr | BreedToken
}

BreedSingular {
  Identifier | SpecialReporter1Args
}

BreedPlural {
  Identifier | SpecialReporter1Args
}

BreedVars {
  Identifier | BreedFirst | BreedMiddle | BreedLast
}

Procedure  {
  To ProcedureName Arguments? ProcedureContent* End
}

ProcedureName {
  Identifier | 
  SpecialCommand | 
  SpecialReporter0Args |
  SpecialReporter1Args |
  SpecialReporter2Args |
  SpecialReporter3Args |
  SpecialReporter4Args |
  SpecialReporter5Args |
  SpecialReporter6Args |
  SpecialReporter1ArgsBoth |
  SpecialReporter0ArgsLink |
  SpecialReporter1ArgsLink |
  SpecialReporter2ArgsTurtle |
  SpecialReporter0ArgsTurtle |
  SpecialReporter1ArgsTurtle |
  SpecialReporter0ArgsLinkP |
  SpecialCommand0Args |
  SpecialCommand1Args |
  SpecialCommand2Args |
  SpecialCommand3Args |
  SpecialCommand4Args |
  SpecialCommand5Args |
  SpecialCommand6Args |
  SpecialCommandCreateTurtle |
  SpecialCommandCreateLink
}

Arguments {
  OpenBracket Identifier* ~statement CloseBracket 
}

ProcedureContent {
  CommandStatement |
  OpenParen CommandStatement CloseParen
}

ReporterContent {
  ReporterStatement ~statement |
  OpenParen ReporterStatement !close CloseParen
}

AnonArguments {
  Arguments | Identifier* ~statement
}

AnonymousProcedure {
  OpenBracket AnonArguments Arrow (ProcedureContent*|ReporterContent*|Value) CloseBracket
}

ShortAnonymousProcedure {
  OpenBracket (ProcedureContent*|ReporterContent*|Value) CloseBracket
}

VariableDeclaration {
  NewVariableDeclaration |
  SetVariable
}

NewVariableDeclaration {
  Let Identifier Value |
  Let Identifier ReporterStatement
}

SetVariable {
  Set VariableName Value |
  Set VariableName ReporterStatement
}

VariableName {
  Identifier | 
  PatchVar | 
  TurtleVar | 
  LinkVar |
  BreedToken
}

Value {
  OpenParen !open Value !close CloseParen |
  String ~statement |
  Constant ~statement |
  VariableName |
  List ~statement |
  Numeric ~statement|
  AnonymousProcedure
}

Literal {
  Numeric ~statement|
  String ~statement|
  Constant ~statement
}

List {
  OpenBracket (Literal | List)* ~statement CloseBracket ~statement
}

CodeBlock {
  OpenBracket ProcedureContent* CloseBracket ~statement
}

ReporterBlock {
  OpenBracket ReporterContent !close CloseBracket |
  OpenBracket Value !close CloseBracket
}

Property {
  OpenBracket VariableName !close CloseBracket
}

ReporterStatement { 
  Arg !leftArgs (ReporterLeft1Args | ReporterLeft1ArgsOpt) !lastArgs Arg+ |
  Arg !leftArgs ReporterLeft2Args Arg !lastArgs Arg+ |
  (SpecialReporter0Args | SpecialReporter0ArgsLink | SpecialReporter0ArgsTurtle | SpecialReporter0ArgsLinkP) !lastArgs Arg* ~statement |
  (SpecialReporter1Args | SpecialReporter1ArgsTurtle | SpecialReporter1ArgsLink | SpecialReporter1ArgsBoth) !lastArgs Arg+ |
  (SpecialReporter2Args | SpecialReporter2ArgsTurtle) Arg !lastArgs Arg+ |
  SpecialReporter3Args Arg Arg !lastArgs Arg+ |
  SpecialReporter4Args Arg Arg Arg !lastArgs Arg+ |
  SpecialReporter5Args Arg Arg Arg Arg !lastArgs Arg+ |
  SpecialReporter6Args Arg Arg Arg Arg Arg !lastArgs Arg+ |
  (Reporter0Args | Reporter0ArgsVar) !lastArgs Arg* ~statement |
  (Reporter1Args | Reporter1ArgsVar | Reporter1ArgsVar0) !lastArgs Arg+ |
  (Reporter2Args | Reporter2ArgsVar | Reporter2ArgsVar0) Arg !lastArgs Arg+ |
  (Reporter3Args | Reporter3ArgsVar) Arg Arg !lastArgs Arg+ |
  (Reporter4Args | Reporter4ArgsVar) Arg Arg Arg !lastArgs Arg+ |
  (Reporter5Args | Reporter5ArgsVar) Arg Arg Arg Arg !lastArgs Arg+ |
  (Reporter6Args | Reporter6ArgsVar) Arg Arg Arg Arg Arg !lastArgs Arg+ |
  OpenParen Reporter0ArgsVar Arg* !close CloseParen |
  OpenParen (Reporter1ArgsVar | ReporterLeft1ArgsOpt) Arg+ !close CloseParen |
  OpenParen Reporter2ArgsVar Arg Arg+ !close CloseParen |
  OpenParen Reporter3ArgsVar Arg Arg Arg+ !close CloseParen |
  OpenParen Reporter4ArgsVar Arg Arg Arg Arg+ !close CloseParen |
  OpenParen Reporter5ArgsVar Arg Arg Arg Arg Arg+ !close CloseParen |
  OpenParen Reporter6ArgsVar Arg Arg Arg Arg Arg Arg+ !close CloseParen |
  OpenParen Reporter2ArgsVar0 Arg* !close CloseParen |
  OpenParen Reporter1ArgsVar0 Arg* !close CloseParen |
  Arg !comparative AndOr Arg |
  APReporter Reporters !lastArgs Arg |
  APReporterFlip Arg !lastArgs Reporters |
  APReporterVar Reporters !lastArgs Arg |
  OpenParen APReporterVar Reporters Arg+ !close CloseParen
}

CommandStatement {
  SpecialCommand0Args !lastArgs Arg* ~statement |
  SpecialCommand1Args !lastArgs Arg+ |
  SpecialCommand2Args Arg !lastArgs Arg+ |
  SpecialCommand3Args Arg Arg !lastArgs Arg+ |
  SpecialCommand4Args Arg Arg Arg !lastArgs Arg+ |
  SpecialCommand5Args Arg Arg Arg Arg !lastArgs Arg+ |
  SpecialCommand6Args Arg Arg Arg Arg Arg !lastArgs Arg+ |
  SpecialCommandCreateTurtle !lastArgs Arg+ |
  SpecialCommandCreateTurtle Arg !lastArgs Arg+ |
  SpecialCommandCreateLink !lastArgs Arg+ |
  SpecialCommandCreateLink Arg !lastArgs Arg+ |
  (Command0Args | Command0ArgsVar) !lastArgs Arg* ~statement |
  (Command1Args | Command1ArgsVar) !lastArgs Arg+ |
  (Command2Args | Command2ArgsVar) Arg !lastArgs Arg+ |
  (Command3Args | Command3ArgsVar | Command3ArgsVar2) Arg Arg !lastArgs Arg+ |
  (Command4Args | Command4ArgsVar) Arg Arg Arg !lastArgs Arg+ |
  (Command5Args | Command5ArgsVar) Arg Arg Arg Arg !lastArgs Arg+ |
  (Command6Args | Command6ArgsVar) Arg Arg Arg Arg Arg !lastArgs Arg+ |
  OpenParen Command0ArgsVar Arg* !close CloseParen|
  OpenParen Command1ArgsVar Arg+ !close CloseParen |
  OpenParen Command2ArgsVar Arg Arg+ !close CloseParen |
  OpenParen Command3ArgsVar2 Arg Arg+ !close CloseParen |
  OpenParen Command3ArgsVar Arg Arg Arg+ !close CloseParen |
  OpenParen Command4ArgsVar Arg Arg Arg Arg+ !close CloseParen |
  OpenParen Command5ArgsVar Arg Arg Arg Arg Arg+ !close CloseParen |
  OpenParen Command6ArgsVar Arg Arg Arg Arg Arg Arg+ !close CloseParen |
  VariableDeclaration |
  Unsupported |
  APCommand !leftArgs Arg !lastArgs Commands |
  OpenParen APCommand Arg+ Commands !close CloseParen
}

Arg {  
  CodeBlock | 
  Value ~statement |
  ReporterContent |
  ReporterBlock |
  Property
}

Commands {
  SpecialCommand | 
  Command0Args |
  Command1Args |
  Command2Args |
  Command3Args |
  Command4Args |
  Command5Args |
  Command6Args |
  Command0ArgsVar |
  Command1ArgsVar |
  Command2ArgsVar |
  Command3ArgsVar |
  Command4ArgsVar |
  Command5ArgsVar |
  Command6ArgsVar |
  Command3ArgsVar2 |
  SpecialCommand0Args |
  SpecialCommand1Args |
  SpecialCommand2Args |
  SpecialCommand3Args |
  SpecialCommand4Args |
  SpecialCommand5Args |
  SpecialCommand6Args |
  SpecialCommandCreateTurtle |
  SpecialCommandCreateLink |
  AnonymousProcedure |
  ShortAnonymousProcedure |
  Identifier
}

Reporters{
  Reporter0Args |
  Reporter1Args |
  Reporter2Args |
  Reporter3Args |
  Reporter4Args |
  Reporter5Args |
  Reporter6Args |
  Reporter0ArgsVar |
  Reporter1ArgsVar |
  Reporter2ArgsVar |
  Reporter3ArgsVar |
  Reporter4ArgsVar |
  Reporter5ArgsVar |
  Reporter6ArgsVar |
  Reporter2ArgsVar0 |
  Reporter1ArgsVar0 |
  ReporterLeft1ArgsOpt |
  ReporterLeft1Args |
  ReporterLeft2Args !leftArgs |
  SpecialReporter0Args |
  SpecialReporter1Args |
  SpecialReporter2Args |
  SpecialReporter3Args |
  SpecialReporter4Args |
  SpecialReporter5Args |
  SpecialReporter6Args |
  SpecialReporter1ArgsBoth |
  SpecialReporter0ArgsLink |
  SpecialReporter1ArgsLink |
  SpecialReporter2ArgsTurtle |
  SpecialReporter0ArgsTurtle |
  SpecialReporter1ArgsTurtle |
  SpecialReporter0ArgsLinkP |
  AnonymousProcedure |
  ShortAnonymousProcedure |
  Identifier
}

@tokens {

  MultiplyDivide {"*" |"/"}

  Exponent {"^"}

  Numeric { $[-]? $[0-9]+ $[\.]? $[0-9]* ($[e] ($[+] | $[-])? $[0-9]+)? | $[-]? $[\.] $[0-9]+ ($[e] ($[+] | $[-])? $[0-9]+)?}
  
  String { '"' (!["\\] | "\\" _)* '"' }

  LineComment { ";" ![\n]* }

  space { $[ \t\n\r]+ }
  
  OpenBracket { "[" }
  
  CloseBracket { "]" }

  Arrow { "->" }

  Comparative { "=" | "!=" | ">" | "<" | "<=" | ">="}

  OpenParen { "(" }

  CloseParen { ")" }

  "[" "]"
}

expression {
  OpenParen expression CloseParen |
  Identifier |
  Command |
  Reporter |
  Numeric |
  String |
  Constant |
  TurtleVar |
  PatchVar |
  LinkVar |
  Unsupported
}

@external tokens keyword from "./tokenizer.js" { 
  ReporterLeft1Args,
  ReporterLeft2Args,
  ReporterLeft1ArgsOpt,  
  GlobalStr, 
  ExtensionStr, 
  BreedStr, 
  PlusMinus,
  Own, 
  Set, 
  Let, 
  To, 
  End, 
  And,
  Or,
  Identifier, 
  Directive,
  Command, 
  Reporter, 
  Extension, 
  TurtleVar, 
  PatchVar, 
  LinkVar, 
  Constant, 
  Unsupported,
  SpecialReporter,
  SpecialCommand,
  BreedToken,
  AndOr,
  APCommand,
  APReporterFlip,
  APReporterVar,
  APReporter
}

@external specialize {Reporter} specializeReporter from './specializer' {
  Reporter0Args,
  Reporter1Args, 
  Reporter2Args, 
  Reporter3Args, 
  Reporter4Args,
  Reporter5Args,
  Reporter6Args,
  Reporter0ArgsVar,
  Reporter1ArgsVar, 
  Reporter2ArgsVar, 
  Reporter3ArgsVar, 
  Reporter4ArgsVar,
  Reporter5ArgsVar,
  Reporter6ArgsVar,
  Reporter2ArgsVar0,
  Reporter1ArgsVar0
}

@external specialize {SpecialReporter} specializeSpecialReporter from './specializer' {
  SpecialReporter0Args,
  SpecialReporter1Args,
  SpecialReporter2Args,
  SpecialReporter3Args,
  SpecialReporter4Args,
  SpecialReporter5Args,
  SpecialReporter6Args,
  SpecialReporter1ArgsBoth,
  SpecialReporter0ArgsLink,
  SpecialReporter1ArgsLink,
  SpecialReporter2ArgsTurtle,
  SpecialReporter0ArgsTurtle,
  SpecialReporter1ArgsTurtle,
  SpecialReporter0ArgsLinkP
}

@external specialize {Command} specializeCommand from './specializer' {
  Command0Args,
  Command1Args, 
  Command2Args, 
  Command3Args, 
  Command4Args,
  Command5Args,
  Command6Args,
  Command0ArgsVar,
  Command1ArgsVar, 
  Command2ArgsVar, 
  Command3ArgsVar, 
  Command4ArgsVar,
  Command5ArgsVar,
  Command6ArgsVar,
  Command3ArgsVar2
}

@external specialize {SpecialCommand} specializeSpecialCommand from './specializer' {
  SpecialCommand0Args,
  SpecialCommand1Args,
  SpecialCommand2Args,
  SpecialCommand3Args,
  SpecialCommand4Args,
  SpecialCommand5Args,
  SpecialCommand6Args,
  SpecialCommandCreateTurtle,
  SpecialCommandCreateLink
}

@external prop context from './props'