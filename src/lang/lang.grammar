@precedence {
  operation @left,
  open @left
}

@top Program { 
  (globalStatement | Unrecognized)*
  Procedure*
}

@detectDelim

@skip { space | LineComment }

Unrecognized {
  expression
}

globalStatement {
  Extensions { ExtensionStr OpenBracket Extension* CloseBracket } |

  Globals { GlobalStr OpenBracket  Identifier* CloseBracket  } |

  Breed { BreedStr OpenBracket Identifier Identifier CloseBracket } |

  BreedsOwn {
    Own OpenBracket Identifier* CloseBracket
  }
}

Procedure {
  To ProcedureName Arguments? ProcedureContent* End
}

ProcedureName {
  Identifier | BreedProceduresReporters
}

Arguments {
  OpenBracket Identifier* CloseBracket
}

ProcedureContent {
  VariableDeclaration | 
  Primitive |
  BreedProceduresReporters | 
  Conditional |
  Commands |
  Reporters |
  Identifier
}

AnonArguments{
  Arguments | Identifier
}

AnonymousProcedure {
  OpenBracket AnonArguments "->" ProcedureContent* CloseBracket
}

VariableDeclaration {
  NewVariableDeclaration |
  SetVariable
}

NewVariableDeclaration {
  Let Identifier Value
}

SetVariable {
  Set VariableName Value
}

VariableName {
  !operation Identifier | !operation PatchVar | !operation TurtleVar | !operation LinkVar
}

Value {
  Reporters
  Calculation |
  String |
  Constant |
  VariableName |
  OpenBracket Value* CloseBracket 
}

NumericValue {
  !operation Constant |
  !operation Numeric |
  !operation VariableName ~statement |
  !operation OpenBracket VariableName !open CloseBracket "of" Identifier
}

Primitive {
  ValFirstPrimitive Value Reporters |
  ValLastPrimitive Reporters Value
}

Reporters {
  Reporter | 
  Reporter0Args |
  Reporter1Args Arg |
  Reporter2Args Arg Arg |
  Reporter3Args Arg Arg Arg |
  Reporter4Args Arg Arg Arg Arg
}

Commands {
  Command |
  Command0Args |
  Command1Args Arg |
  Command2Args Arg Arg |
  Command3Args Arg Arg Arg |
  Command4Args Arg Arg Arg Arg
}

Arg {   
  Boolean |
  Calculation |
  Value |
  NumericValue ~statement |
  Primitive |
  AnonymousProcedure |
  VariableName ~statement | 
  Reporters |
  Commands |
  OpenBracket ProcedureContent+ CloseBracket
}

BreedProceduresReporters {
  BreedFirst | BreedMiddle | BreedLast
}

Calculation {
  NumericValue ~statement |
  Calculation !operation Operation Calculation |
  !operation "(" Calculation ")"
}

Boolean {
  Value !operation Comparative Value |
  "(" Boolean ")" |
  Boolean !operation And Boolean |
  Boolean !operation Or Boolean |
  VariableName |
  "not" Value
}

Conditional {
  "if" Boolean OpenBracket ProcedureContent* CloseBracket |
  "(" "ifelse-value" IfValue+ ElseValue ")" |
  "ifelse-value" IfValue ElseValue |
  "ifelse" Boolean OpenBracket ProcedureContent* CloseBracket OpenBracket ProcedureContent* CloseBracket
}

IfValue {
  Boolean OpenBracket Value CloseBracket
}

ElseValue {
  OpenBracket  Value !operation CloseBracket
}

@tokens {
  Numeric { $[0-9]+ $[\.]? $[0-9]* }
  
  String { '"' (!["\\] | "\\" _)* '"' }

  LineComment { ";" ![\n]* }

  Operation {
    "+" | "-" | "/" | "*" | "^"
  }

  Comparative {
    "=" | "!=" | "<" | ">" | "<=" | ">="
  }

  space { $[ \t\n\r]+ }
  
  OpenBracket { "[" }
  
  CloseBracket { "]" }

  "[" "]"
}

expression {
  "(" expression ")" |
  Identifier |
  Command |
  Reporter |
  Numeric |
  String |
  Constant |
  TurtleVar |
  PatchVar |
  LinkVar |
  Unsupported
}

@external tokens keyword from "./tokenizer.js" { 
  Command0Args,
  Command1Args, 
  Command2Args, 
  Command3Args, 
  Command4Args, 
  Reporter0Args,
  Reporter1Args, 
  Reporter2Args, 
  Reporter3Args, 
  Reporter4Args, 
  And, 
  Or, 
  GlobalStr, 
  ExtensionStr, 
  BreedStr, 
  BreedFirst, 
  BreedLast, 
  BreedMiddle, 
  Own, 
  Set, 
  Let, 
  To, 
  End, 
  ValFirstPrimitive, 
  ValLastPrimitive, 
  Identifier, 
  Directive, 
  Command, 
  Reporter, 
  Extension, 
  TurtleVar, 
  PatchVar, 
  LinkVar, 
  Constant, 
  Unsupported
}